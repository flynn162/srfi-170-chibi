(import (scheme base)
        (scheme write)
        (scheme cxr)
        (only (srfi 95) sort!)
        (srfi 69)
        (only (chibi process) process->sexp)
        )

(define (writeln object)
  (write object)
  (newline))

(define (displayln object)
  (display object)
  (newline))

(define (display-enum current-integer names)
  (for-each
   (lambda (symb)
     (writeln `(define ,symb ,current-integer))
     (set! current-integer (+ 1 current-integer)))
   names))

(define (prefix-with prefix fields)
  (map
   (lambda (attr)
     (string->symbol (string-append prefix (symbol->string attr)))
     )
   fields))

(define (display-change-of-prefix from-prefix to-prefix fields)
  (for-each
   (lambda (symb)
     (writeln
      `(define
         ,(string->symbol (string-append from-prefix (symbol->string symb)))
         ,(string->symbol (string-append to-prefix (symbol->string symb)))
         )
      ))
   fields))

(define (remove-duplicates input-list key-getter value-getter)
  (let ((ht (make-hash-table)))
    (for-each
     (lambda (element)
       (let ((key (key-getter element)))
         (if (not (or (hash-table-exists? ht key)
                      (member (value-getter element) '(ELAST))))
             (hash-table-set! ht key element))
         ))
     input-list)
    (hash-table-values ht)
    ))

(define enum:port-types
  '(binary-input textual-input binary-output textual-output
                 binary-input/output))

(define enum:buffering-modes
  '(buffer-none buffer-block buffer-line))

(define fields:file-info
  '(device inode mode nlinks uid gid rdev size blksize blocks
           atime mtime ctime))

(define predicates:file-info
  '(directory? fifo? symlink? regular? socket? device?))

(define methods:user
  '(uid gid effective-uid effective-gid supplementary-gids))

(define fields:user-info
  '(name uid gid home-dir shell full-name parsed-full-name))

(define exports
  `(export
    ;; 3.1

    posix-error? posix-error-name posix-error-message
    ;; non-standard:
    posix-error-number

    ;; 3.2

    (unquote-splicing enum:port-types)
    (unquote-splicing enum:buffering-modes)
    open-file fd->port

    ;; 3.3

    create-directory create-fifo create-hard-link create-symlink read-symlink
    rename-file delete-directory set-file-owner set-file-times truncate-file

    file-info file-info?
    ,@(prefix-with "file-info:" fields:file-info)
    ,@(prefix-with "file-info-" predicates:file-info)

    set-file-mode directory-files make-directory-files-generator
    open-directory read-directory close-directory
    real-path file-space temp-file-prefix create-temp-file
    call-with-temporary-filename

    ;; 3.5

    umask set-umask! current-directory set-current-directory! pid nice
    ,@(prefix-with "user-" methods:user)

    ;; 3.6

    user-info user-info?
    ,@(prefix-with "user-info:" fields:user-info)

    group-info group-info?
    group-info:name group-info:gid

    ;; 3.10

    posix-time monotonic-time

    ;; 3.11

    get-environment-variables get-environment-variable
    set-environment-variable! delete-environment-variable!

    ;; 3.12

    terminal?

    ;; For debugging purposes

    pa-errno pa-strerror
    ))

(define (generate-sld)
  (displayln ";; -*- scheme -*-")
  (displayln ";; This file is automatically generated")
  (displayln "(define-library (170)")
  (writeln
   '(import (scheme base)
            (scheme case-lambda)
            (prefix (scheme process-context) r7rs:)
            (prefix (chibi filesystem) cs:)
            (prefix (chibi system) cs:)
            (prefix (chibi process) cs:)
            (only (srfi 133) vector-binary-search)
            ))
  (writeln '(include-shared "./170/posix"))
  (writeln '(include "./170/wrap.scm"))
  (writeln exports)
  (displayln "(begin")

  (display-enum #x10 enum:port-types)
  (display-enum #x20 enum:buffering-modes)

  (let*
      ((errors (cdr (process->sexp "tools/GenerateErrorNames")))
       (errors (remove-duplicates errors caddr cadr))
       (errors (list->vector errors))
       )
    (sort! errors < caddr)
    (writeln `(define errno-values ,(vector-map caddr errors)))
    (writeln `(define errno-names ,(vector-map cadr errors)))
    )

  (displayln "))")
  )

(generate-sld)
